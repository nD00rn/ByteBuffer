
plugins {
    id 'java'
    id 'maven'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'signing'
}

group = "com.github.nd00rn"
archivesBaseName = "bytebuffer"
version = "0.0.1"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'bytebuffer'
                packaging 'jar'
                description 'A simple wrapper around byte list'
                url 'https://github.com/nd00rn/ByteBuffer'

                scm {
                    connection 'scm:git:https://github.com/nD00rn/ByteBuffer.git'
                    developerConnection 'scm:git:https://github.com/nD00rn/ByteBuffer.git'
                    url 'https://github.com/nD00rn/ByteBuffer'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://github.com/nD00rn/ByteBuffer/blob/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id 'nD00rn'
                        name 'Niels van Doorn'
                        email 'ndoorn@outlook.com'
                    }
                }
            }
        }
    }
}
